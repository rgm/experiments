(ns user
  "Try using redis streams as the ledger/backbone for a commander architecture.

   - https://www.youtube.com/watch?v=30Odhss1xVA
   - https://www.youtube.com/watch?v=B1-gS0oEtYc
   - https://www.youtube.com/watch?v=qDNPQo9UmJA"
  (:require
   [clojure.pprint :as pp]
   [clojure.core.async :as a]
   [clojure.string :as str]
   [integrant.core :as ig]
   [integrant.repl :refer [halt reset]]
   [malli.core :as m]
   [malli.dev.pretty :as pretty]
   [malli.util :as mu]
   [portal]
   [taoensso.carmine :as car :refer [wcar]]
   [taoensso.timbre :as timbre]))

;; DRAMATIS PERSONAE:
;; evt ......... Event, a map
;; cmd ......... Command, a map
;; ci-evt ...... Command Issuance, an event with an issued-command payload
;; ca-evt ...... Command Acceptance, an event with an accepted-command payload
;; cr-evt ...... Command Rejection, an event with a rejected-command payload
;; eid ......... Event ID, a timestamp string in Redis streams
;;               (see https://redis.io/docs/data-types/streams/#entry-ids
;; cid ......... Command ID, a uuid (so that it can be generated by a client)
;; ts .......... Timestamp, an instant

;;
;; schemas
;;

(defn validate! [spec value]
  (when-not (m/validate spec value)
    (pp/pprint value)
    (pretty/explain spec value)
    (let [data {:reason ::malli-failure
                :value value
                :spec spec
                :err (m/explain spec value)}]
      (throw (ex-info "malli failure" data)))))

(def schema:evt-name symbol?)

(def schema:cmd-name symbol?)

(def schema:evt
  "Events are records of effects that have happened in the system, for
   secondary reactions (eg. updating the UI) or updating derived data
   (eg. the audit log on a project).

   The only kind of event that can lead to a system mutation is a
   `:command/issued` event. (Note the lower case and passive voice).

   Possible uses for metadata:
   - including a set of audience user-ids to let downstream
   processes know how to distribute messages;
   - ..."
  (let [user-id string?]
    [:map {:closed true}
     [:evt/name      schema:evt-name]
     [:evt/author-id user-id]
     [:evt/data      :map]
     [:evt/metadata  {:optional true} :map]
     ;; id, parent-id, ts optional b/c needs id from redis
     [:evt/parent-id {:optional true} :string]
     [:evt/id        {:optional true} :string]
     [:evt/ts        {:optional true} inst?]]))

(def schema:cmd
  "Commands are expressions of a user's intent, as data.

   They are observable in the event ledger as (issued, accepted) or
   (issued, rejected) pairs of events. (On the front-end, we might see a
   transient (issued, bounced) pair, signifying something didn't even make
   it into the ledger usually for authz reasons).

   They flow into the ledger, where the fact of a command is quickly persisted
   as an event. A command/issued event wrapping the command flows into the
   commander, where it's acted on, and will lead to either an accepted event or
   a rejected event (plus some optional other side events that happened. Note
   that the commander issuing a command in response is also a kind of side
   event :mindblown:).

   Commands are named with all-caps imperative voice, like :DO-A-THING!"
  [:map {:closed true}
   [:cmd/id   uuid?]
   [:cmd/name schema:cmd-name]
   [:cmd/data {:optional true} :map]])

(def schema:issued-cmd
  "Regardless of whether a command is ultimately accepted or rejected,
   processing is kicked off by recording the fact that it was issued (which is
   always true).
   "
  (mu/merge schema:evt
            [:map
             [:evt/name [:= 'command/issued]]
             [:evt/data schema:cmd]]))

;;
;; ledger
;;

(defn redis-entry-id->ts [entry-id]
  (let [ms (first (str/split entry-id #"-"))]
    (java.time.Instant/ofEpochMilli (parse-long ms))))

(defn redis-entry->evt [re]
  (let [[eid [_ evt]] re]
    (assoc evt :evt/id eid :evt/ts (redis-entry-id->ts eid))))

(defn get-all-evts [ledger]
  ;; nb. redis stream ids are timestamps, just derived from redis entry id
  ;; so extract an inst to get a ts; we don't need to record this separately
  (->> (wcar (:redis ledger) (car/xrange (:stream ledger) "-" "+"))
       (map redis-entry->evt)))

(defprotocol ILedger
  (start-delivering! [this] [this last-eid]
    "Start the ledger distributing new events")
  (stop-delivering! [this]
    "Stop the ledger from distributing new events")
  (delivering? [this]
    "Is the ledger delivering events?")
  (push-event [this evts]
    "Add an event to the ledger for distribution.")
  (listen-for-events [this chan]
    "Connect chan to the ledger for delivery of new events.")
  (stop-listening-for-events [this chan]
    "Disconnect chan from the ledger.")
  (trim-events [this eid]
    "Trim old events before eid."))

(defn read-events
  "Read events from a Redis stream.

   The call will block until the end of timeout, and will:
   - succeed with valid events;
   - return nil if no event arrived during the timeout; or
   - throw if the event that was pulled from the stream was not valid.

   Pass 0 for timeout to wait forever. Pass $ for last-eid to get the 'next'
   event."
  [redis stream batch-size timeout-ms last-eid]
  (when-let [redis-entries (wcar redis (car/xread
                                        "COUNT" batch-size
                                        "BLOCK" timeout-ms
                                        "STREAMS" stream
                                        last-eid))]
    (let [evts (->> redis-entries first second (map redis-entry->evt))]
      (doseq [evt evts] (validate! schema:evt evt))
      evts)))

(defrecord RedisLedger [redis stream pub-chan mult-chan *state]
  ;; conceal redis's presence behind a facade ... the ledger's job is only to
  ;; persist events and then pass them on, not to act on them
  ;; pass in a configured channel; the delivery loop will throw events onto it
  ;; as they occur
  ILedger
  (start-delivering! [this]
    (start-delivering! this "$")) ;; "$" in redis is "last event"
  (start-delivering! [_ last-eid]
    (timbre/debug "starting delivery loop")
    (swap! *state assoc :delivering? true)
    ;; block on redis reads on a separate thread and ship events off on the
    ;; channel when they arrive
    (a/thread
     (loop [last-eid last-eid]
       (if (:delivering? @*state)
         (do (timbre/debug "ledger waiting for event")
             (let [batch-size 1
                   timeout-ms 0 ;; wait "forever"
                   evts (read-events redis stream batch-size timeout-ms
                                     last-eid)]
               (timbre/debug "delivering event")
               (doseq [evt evts] (a/>!! pub-chan evt))
               (recur (-> evts last :evt/id))))
         ;; there's a bug here:
         ;; if we stop delivery and no event comes in to flush out the queue,
         ;; we'll have 2 loops running
         (do (timbre/debug "stopping delivery loop")
             (swap! *state assoc :delivering? false)
             :done))))
    :started)

  (stop-delivering! [_]
    ;; nb. will keep delivering til the last XREAD timeout expires with nil or
    ;; possibly one last event
    (timbre/debug "marking delivery loop as stopped")
    (swap! *state assoc :delivering? false)
    :stopped)

  (delivering? [_]
    (boolean (:delivering? @*state)))

  (push-event [_ evt]
    (timbre/debug "pushing evt to redis")
    (validate! schema:evt evt)
    (wcar redis (car/xadd stream
                          ; "MAXLEN" "~" 10000 ;; uncomment to cap stream size
                          "*"
                          "evt"
                          ;; lose any provided timestamp since redis stream ids
                          ;; will be the source of truth here
                          (dissoc evt :evt/ts))))

  (listen-for-events [_ chan]
    ;; audience fan-out using mult/tap
    ;; nb. the provided chan can slow down the mult; make sure they're buffered
    ;; correctly. Provide transducers on the channels to filter for specific
    ;; events.
    (timbre/debug "connecting" chan "to ledger mult")
    (a/tap mult-chan chan)
    chan)

  (stop-listening-for-events [_ chan]
    (timbre/debug "disconnecting" chan "from ledger mult")
    (a/untap mult-chan chan)
    chan)

  (trim-events [_ eid]
    ;; car/xtrim seems to have trouble rn, so use redis-call
    ;; https://github.com/taoensso/carmine/issues/283
    (wcar redis (car/redis-call [:xtrim stream "MINID" "~" eid]))))

;;
;; integrant
;;

(def system-config
  {::portal              {}
   ::state               {:counter 0}
   ::redis               {:uri "redis://localhost:6379/2"}
   ::ledger              {:redis (ig/ref ::redis) :stream "myledger"}
   ::commander           {:ledger (ig/ref ::ledger) :*state (ig/ref ::state)}
   #_#_::consumer-1-fizzbuzz {:ledger (ig/ref ::ledger) :*state (ig/ref ::state)}
   #_#_::consumer-2-archiver {:ledger (ig/ref ::ledger) :*state (ig/ref ::state)}})

;; "state" component
;; system state would usually be a postgres db or such; just an atom here
;; for illustration

(defmethod ig/init-key ::state
  [_ {:keys [counter]}]
  (atom {:counter counter
         ;; last event-ids processed
         ;; (in practice these should be persisted someplace. Here we'll
         ;; restart the commander from earliest message every time the system
         ;; starts up).
         :last-eids {::commander 0
                     ::consumer-1-fizzbuzz 0
                     ::consumer-2-archiver 0}}))

(defmethod ig/init-key ::portal [_ _] (portal/start-portal))
(defmethod ig/halt-key! ::portal [_ _] (portal/stop-portal))

;; redis component

(defmethod ig/init-key ::redis
  [_ {:keys [uri]}]
  (let [opts {:pool (car/connection-pool {}) :spec {:uri uri}}]
    (when-not (= "PONG" (wcar opts (car/ping)))
      (throw (ex-info "can't communicate with redis" opts)))
    opts))

;; ledger component

(defmethod ig/init-key ::ledger
  [_ {:keys [stream redis]}]
  ;; a sliding buffer is arguably what we want: at a certain point we're
  ;; interested in novelty and if downstream can't keep up we sacrifice the old
  ;; events
  (let [pub-chan (a/chan (a/sliding-buffer 256))
        ledger (map->RedisLedger {:redis redis
                                  :stream stream
                                  :pub-chan pub-chan
                                  :mult-chan (a/mult pub-chan)
                                  :*state (atom {})})]
    (start-delivering! ledger)
    ledger))

(defmethod ig/halt-key! ::ledger
  [_ this]
  (a/close! (:chan this))
  (stop-delivering! this))

;; commander component

(defn get-cmd-name [evt] (get-in evt [:evt/data :cmd/name]))

(defn get-cmd-id [evt] (get-in evt [:evt/data :cmd/id]))

(defn make-child-evt
  [author-id evt child-evt-name data]
  #:evt{:name      child-evt-name
        :author-id author-id
        :parent-id (:evt/id evt)
        :data      data})

(defn make-cmd-rejection-evt
  [author-id evt reason data]
  (make-child-evt author-id
                  evt
                  'command/rejected
                  (merge data
                         {:cmd/id (get-cmd-id evt)
                          :cmd/name (get-cmd-name evt)
                          ::reason reason})))

(defn make-cmd-acceptance-evt
  [author-id evt data]
  (make-child-evt author-id
                  evt
                  'command/accepted
                  (merge data
                         {:cmd/id (get-cmd-id evt)
                          :cmd/name (get-cmd-name evt)})))

(defmulti transact!
  "Mutate system state in response to an issued command."
  (fn [_author-id _*state evt] (tap> evt) (get-cmd-name evt)))

(defmethod transact! :default
  [author-id _ evt]
  ;; Default action is to reject the command and not change system state.
  ;;
  ;; We allow-list new commands by implementing new multimethods. This lets us
  ;; put the method bodies closer to the code for the specific domain.
  [(make-cmd-rejection-evt author-id evt "no-handler-for-cmd" {})])

(defn matches-evt?
  [evts]
  (let [pred (set evts)]
    (validate! [:set schema:evt-name] pred)
    (comp pred :evt/name)))

(defn process-command! [*state evt]
  (timbre/debug "transacting command" (get-cmd-name evt))
  (try
    (let [evts (transact! "commander" *state evt)]
      (if (m/validate [:sequential schema:evt] evts)
        evts
        (throw (ex-info "didn't get events from transact!"
                        {:return-value evts}))))
    ;; handle normal command processing rejection using exception control flow
    ;; so that we make sure the commander loop is airtight and we don't have
    ;; loop death
    (catch Exception ex
      [(make-cmd-rejection-evt "commander"
                               evt
                               "exception-in-transact!"
                               {:message (ex-message ex)
                                :data (ex-data ex)})])))

#_(defn list-commands [ledger]
    (let [events (list-events ledger)]))

(defmethod ig/init-key ::commander
  ;; the commander's job is to act on command-issued events
  ;; it provides a channel for the ledger to stuff new events into
  ;; we use a filter on the channel to pay attention to only issued commands
  [_ {:keys [*state ledger]}]
  (timbre/info "starting commander component")
  (let [xf (filter (matches-evt? #{'command/issued}))
        chan (a/chan 1 xf)
        _ (listen-for-events ledger chan)
        worker (a/go-loop []
                 (timbre/warn "got here 1" chan)
                 (when-some [evt (a/<! chan)]
                   (timbre/warn "got here 2")
                   (doseq [evt (process-command! *state evt)]
                     (push-event ledger evt))
                   (recur)))]

    {:chan chan :worker worker :ledger ledger}))

(defmethod ig/halt-key! ::commander
  [_ this]
  ; (stop-listening-for-events (:ledger this) (:chan this))
  (a/close! (:chan this)))

;; example consumer component
;; eg a logger that fires when the counter achieves fizzbuzz

(defmethod ig/init-key ::consumer-1-fizzbuzz
  [_ {:keys [ledger *state]}]
  (let [chan (a/chan (a/dropping-buffer 8)
                     (filter (constantly true)))]
    (listen-for-events ledger chan)))

;; example consumer component
;; eg a process that persists old events someplace else and trims the old event
;; stream so it doesn't grow without bound in redis

(defmethod ig/init-key ::consumer-2-archiver
  [_ {:keys [ledger *state]}]
  (let [chan (a/chan (a/dropping-buffer 8)
                     (filter (constantly true)))]
    (listen-for-events ledger chan)))

(defn go []
  (integrant.repl/set-prep! #(ig/prep system-config))
  (integrant.repl/go))

;;
;; Example of the specific domain of an incrementing counter
;;

(defmethod transact! 'INCREMENT-COUNTER!
  [author-id *state evt]
  (timbre/debug "processing 'INCREMENT-COUNTER!")
  (let [parent-id (:evt/id evt)
        cmd-name (get-cmd-name evt)
        authzd? (= (:evt/author-id evt) "authorized-author")]
    ;; increment the counter here if authzd
    (when-not authzd?
      (throw (ex-info "not authorized" {:evt/author-id (:evt/author-id evt)
                                        :evt/id parent-id
                                        :cmd/name cmd-name})))
    ;; actually execute the mutation
    (swap! *state update :counter inc)

    ;; return for recording in the ledger:
    ;; 1. an event recording that the counter incremented
    ;; 2. an event recording that the command was accepted by the commander
    [(make-child-evt author-id evt 'counter/incremented {})
     (make-cmd-acceptance-evt author-id evt {})]))

(defn increment-counter!
  "Issue a command to increment the counter."
  [ledger author-id]
  (push-event ledger
              #:evt{:name 'command/issued
                    :author-id author-id
                    :data #:cmd{:name 'INCREMENT-COUNTER!
                                :data {}}}))

(comment
  ;; https://stackoverflow.com/a/27056185
  (alter-var-root #'*out* (constantly *out*))
  (try (go) (catch Exception ex (print (ex-data ex))))
  (halt)
  (try (reset) (catch Exception ex (tap> (ex-data ex))))
  (do
    #_(.addShutdownHook (Runtime/getRuntime)
                        (Thread. #(do (timbre/info "stopping system")
                                      (halt))))
    (go)
    (require '[integrant.repl.state :as irs])
    (def redis (::redis irs/system))
    (def ledger (::ledger irs/system)))

  (delivering? ledger)
  (start-delivering! ledger)
  (stop-delivering! ledger)

  ;; use futures or you'll block your repl
  (future (tap> (read-events redis "myledger" 1 0 "$")))
  (future (tap> (a/<!! (:chan ledger))))

  (increment-counter! ledger "authorized-author")
  (increment-counter! ledger "unauthorized-author")
  (last (get-all-evts ledger))
  (:*state ledger)
  (-> irs/system ::state deref :counter))

;; todo
;; - get command rejection working
